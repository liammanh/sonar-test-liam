plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id "org.sonarqube" version "3.5.0.2730"
    id 'jacoco'
}

sonarqube {
    properties {
        property "sonar.projectKey", "liammanh_sonar-test-liam"
        property "sonar.organization", "liammanh"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.android.lint.reportPaths", "${project.buildDir}/reports/lint-results-debug.xml"
        property "sonar.junit.reportPaths", "${project.buildDir}/test-results/testDebugUnitTest/"
        property "onar.jacoco.reportPaths", "${project.buildDir}/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec"
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
        property 'sonar.coverage.jacoco.xmlReportPaths', "${project.buildDir}/reports/jacocoTestReport/jacocoTestReport.xml"

        property "sonar.sources", "src/main/java,src/main/res"
        property "sonar.tests", "src/test/java" //src/androidTest/java

    }
}


android {
    compileSdk 32

    defaultConfig {
        applicationId "com.gogox.sonarexample"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

task jacocoTestReport(type: JacocoReport,
        dependsOn: ["testDebugUnitTest",
        ]) {//'createDebugCoverageReport' -- run android test
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    reports {
        xml.required = true
        csv.required = false
    }

    def fileFilter = ['android/**/*.*',
                      '**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      '**/*Module.*', // modules for Dagger.
                      '**/*Module$Companion.*', // modules for Dagger+Kotlin.
                      '**/*Dagger*.*', // Dagger auto-generated code.
                      '**/*MembersInjector*.*', // Dagger auto-generated code.
                      '**/*_Provide*Factory*.*',
                      '**/*_Factory.*',] // Dagger auto-generated code
    def debugTree = fileTree(
            dir: "$project.buildDir/tmp/kotlin-classes/debug",
            excludes: fileFilter
    )
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: "${project.buildDir}", includes: [
            "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
            "outputs/code_coverage/debugAndroidTest/connected/*/coverage.ec"
    ])
}

task jacocoTestCoverageVerification(type: JacocoCoverageVerification, dependsOn: 'jacocoTestReport') {
    violationRules {
        rule {
            limit {
                minimum = "0.5".toBigDecimal()
            }
        }

        rule {
            element = "CLASS"
            //includes = listOf("org.gradle.*")

            limit {
                counter = "LINE"
                value = "TOTALCOUNT"
                maximum = "0.3".toBigDecimal()
            }
        }
    }
}


dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}